apply plugin: 'org.springframework.boot'
apply from: 'https://dl.bintray.com/scalding/generic/waroverlay.gradle'
apply from: 'https://raw.githubusercontent.com/apereo/cas/5.2.x/gradle/overrides.gradle'

apply plugin: 'eclipse'
apply plugin: 'idea'

configurations {
    compile.exclude group: "org.apache.directory.api", module: "api-ldap-schema-data"
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

bootRepackage {
    //mainClass = "org.apereo.cas.web.CasWebApplication"
    mainClass = "org.ow2.proactive.iam.bootstrap.BootstrapApplication"
    executable = true
}

bootRun {
    addResources = true
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
}

dependencies {
    //compile "org.apereo.cas:cas-server-webapp-tomcat:${project.'cas.version'}@war"
    compile "org.apereo.cas:cas-server-webapp-jetty:${project.'cas.version'}@war"
    compile "org.apereo.cas:cas-server-webapp-init:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-support-ldap:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-support-json-service-registry:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-support-rest:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-support-rest-tokens:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-support-token-tickets:${project.'cas.version'}"

    //compile group: 'org.apache.directory.server', name: 'apacheds-all', version: '2.0.0-M24'
    compile group: 'org.apache.directory.api', name: 'api-all', version: '1.0.0'
    compile group: 'org.apache.directory.server', name: 'apacheds-core-api', version: '2.0.0-M24'
    compile group: 'org.apache.directory.server', name: 'apacheds-protocol-shared', version: '2.0.0-M24'
    compile group: 'org.apache.directory.server', name: 'apacheds-core-annotations', version: '2.0.0-M24'
    compile group: 'org.apache.directory.server', name: 'apacheds-protocol-ldap', version: '2.0.0-M24'

    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
}

task copyConfig(type: Copy, description: "Copy CAS configuration over to /etc/cas/config") {
    doLast {
        from "${project.rootDir}/etc/cas/config"
        into '/etc/cas/config'

        println "Copied configuration files into /etc/cas/config"
    }
}

war {
    dependsOn copyConfig
    baseName 'cas'
    includeWarJars = true
    entryCompression = ZipEntryCompression.STORED
}

task explodeWar(type: Copy, group: "build", description: "Explodes the cas.war") {
    doLast {
        from zipTree(project.war.outputs.files.singleFile)
        into "${buildDir}/cas"
        println "CAS web application artifact exploded into [cas/build/cas]"
    }
}

task run(group: "build", description: "Run the CAS web application in embedded container mode") {
    dependsOn build
    doLast {
        def casRunArgs = Arrays.asList(project.'cas.run.jvmArgs'.split(" "))
        javaexec {
            main = "-jar";
            jvmArgs = casRunArgs
            args = ["build/libs/cas.war"]

            logger.info "Started ${commandLine}"
        }
    }
}

task debug(group: "build", description: "Debug the CAS web application in embedded mode on port 5005") {
    dependsOn build
    doLast {
        def casDebugArgs = Arrays.asList(project.'cas.debug.jvmArgs'.split(" "))

        javaexec {
            main = "-jar";
            jvmArgs = casDebugArgs
            args = ["build/libs/cas.war"]

            logger.info "Started ${commandLine}"
        }
    }
}
